#!/opt/perl/bin/perl

use 5.010;

use strict;
use warnings;
no  warnings 'syntax';

my $MIN_VERSION = "5.20.0";
my @MIN_VERSION = split /\./ => $MIN_VERSION;

my $PROVE = "/opt/perl/bin/prove";

my @perls = `ls /opt/perl/bin/perl5*`;
chomp @perls;

@perls = map  {$$_ [0]}
         sort {$$a [1] <=> $$b [1] ||
               $$a [2] <=> $$b [2]}
         grep {$$_ [1] >  $MIN_VERSION [1] ||
               $$_ [1] == $MIN_VERSION [1] && $$_ [2] >= $MIN_VERSION [2]}
         map  {[$_, do {/perl5\.([0-9]+)\.([0-9]+)/; ($1, $2)}]} @perls;

foreach my $perl (@perls) {
    say "Running prove for $perl:";
    system $PROVE, "--exec", "$perl -Ilib", "-lr", "t";
    say "-" x 80;
}

__END__
